plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    // Sqlite JDBC driver and Hibernate ORM
    implementation 'org.xerial:sqlite-jdbc:3.47.2.0'
    implementation 'org.hibernate.orm:hibernate-core:6.6.4.Final'
    implementation 'org.hibernate.orm:hibernate-community-dialects:6.6.4.Final'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'  // Required for some Hibernate annotations

    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

